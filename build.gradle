plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs' version '5.0.13'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

dependencies {
    implementation 'org.seleniumhq.selenium:selenium-java:4.20.0'
    implementation 'io.github.bonigarcia:webdrivermanager:5.8.0'
    implementation 'com.google.inject:guice:7.0.0'
    implementation 'org.jsoup:jsoup:1.17.2'

    // Переключаемся на Cucumber 6.11.0
    implementation 'io.cucumber:cucumber-guice:6.11.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'io.cucumber:cucumber-java:6.11.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:6.11.0'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Test).configureEach {
    // Устанавливаем кодировку по умолчанию и пробрасываем browser
    systemProperty 'file.encoding', 'UTF-8'
    systemProperty 'browser', System.getProperty('browser', 'chrome')
    jvmArgs '-Dfile.encoding=UTF-8'
}



task cucumber(type: Test) {
    group = 'verification'
    description = 'Запустить только Cucumber BDD сценарии'
    useJUnitPlatform {
        includeEngines 'cucumber'
    }
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

check.dependsOn cucumber

checkstyle {
    configFile = file("linters/checkstyle.xml")
    toolVersion = "10.12.3"
}

spotbugs {
    toolVersion = "4.8.3"
    effort = 'max'
    reportLevel = 'low'
    excludeFilter = file("linters/spotbugs-exclude.xml")
}
